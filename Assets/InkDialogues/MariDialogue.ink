INCLUDE globals.ink

{ liho_banished:-> end_dialogue}
{ mari_met:-> meet }
-> first_meet


=== first_meet ===
~mari_met = true
Добрый вечер, Фёдор, я вас заждалась, все люди уже разошлись.#author:mari
+ Извините, что пришёл так поздно#author:fedor
- Впрочем неважно, вы же разберётесь с дьяволом?#author:mari
* Да, я собрал всё необходимое#author:fedor
- Вот ключи от подвала, избавьте меня от этого демона поскорей, он очень мешает работе кабака.#author:mari
~ take_keys = true
+ Чем же он так мешает?#author:fedor
+ [Забрать ключи и уйти] -> END
- Кухарки боятся спускаться за продуктами и идут туда только тогда, когда уже совсем ничего не осталось. #author:mari
+ Хмм, а они рассказывали, что их так пугает? #author:fedor
-> details
+ Ну что ж, пойду посмотрю, что там такого жуткого. #author:fedor
-> END


=== meet ===
Ну что, получилось избавиться от демона?#author:mari
+{ not liho_banished } { know_thing } Расскажите, пожалуйста, ещё раз: что говорят кухарки? #author:fedor
-> details
+{ not liho_banished } { not know_thing } Что рассказывают кухарки? #author:fedor
-> details
+{ liho_banished }  Да, я закончил. #author:fedor
-> END


=== details ===
~ know_thing = true
Они говорят, что слышали, как кто-то ходит в подвале, чего быть не может, ведь мы всегда закрываем его на замок.#author:mari
+ [* Слушать *]
- Одна из них твердит, что видела кого-то в углу подвала, после этого она ни в какую не хочет спускаться в подвал.#author:mari
+ [* Слушать *]
- Те, кто заходил туда, рассказывают, что после этого весь день их преследовали неудачи. Уже даже не знаю, правда это или просто суеверие. #author:mari
+ [* Слушать *]
- Фёдор, пожалуйста, изабвьте меня от этой беды.#author:mari
+ Я постараюсь сделать всё возможное -> END


=== beer ===
Идите спать, Фёдор. Кабак закрывается.#author:mari
+ [Уйти] -> END

=== end_dialogue ===
Ну что, получилось избавиться от демона?#author:mari
+ Да, это было Лихо, я его изгнал.#author:fedor
- Спасибо за помощь! Вот ваши пять рублей. #author:mari
+ [* Взять деньги *]
+ Раз уж работа сделана, то можно и выпить пива. Налейте мне кружку. #author:fedor
-> beer
- Теперь мне надо идти домой#author:fedor
+ [Уйти] -> END